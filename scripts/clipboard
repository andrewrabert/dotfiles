#!/usr/bin/env zsh
case "$1" in
    copy|paste)
        MODE="$1"
        ;;
    *)
        echo 'usage: clipboard (copy|paste)'
        exit 1
esac

clipboard_wayland() {
    if [ -n "$WAYLAND_DISPLAY" ]; then
        case "$MODE" in
            copy)
                wl-copy
                ;;
            paste)
                wl-paste
                ;;
        esac
    else
        return 1
    fi
}


clipboard_xorg() {
    if [ -n "$XAUTHORITY" ]; then
        case "$MODE" in
            copy)
                xclip -selection c
                ;;
            paste)
                xclip -selection c -o
                ;;
        esac
    else
        return 1
    fi
}


clipboard_macos() {
    if command -v pbcopy > /dev/null; then
        case "$MODE" in
            copy)
                pbcopy
                ;;
            paste)
                pbpaste
                ;;
        esac
    else
        return 1
    fi
}


clipboard_tmux() {
    if command -v tmux > /dev/null; then
        case "$MODE" in
            copy)
                tmux load-buffer -
                ;;
            paste)
                tmux save-buffer -
                ;;
        esac
    else
        return 1
    fi
}


clipboard_termux() {
    if command -v termux-clipboard-get > /dev/null; then
        case "$MODE" in
            copy)
                termux-clipboard-set
                ;;
            paste)
                termux-clipboard-get
                ;;
        esac
    else
        return 1
    fi
}


if [ "$MODE" = 'copy' ]; then
    tee --output-error=warn \
        >(clipboard_macos) \
        >(clipboard_termux) \
        >(clipboard_tmux) \
        >(clipboard_wayland) \
        >(clipboard_xorg) \
        > /dev/null 2> /dev/null
    exit 0
else
    clipboard_wayland \
        || clipboard_xorg \
        || clipboard_macos \
        || clipboard_termux \
        || clipboard_tmux \
        || (echo error: no suitable clipboard command detected >&2; exit 1)
fi
