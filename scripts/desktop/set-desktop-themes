#!/usr/bin/env zsh
set -e

CURRENT_THEME=~/.setdesktoptheme

show_help() {
    cat << EOF
usage: set-desktop-themes [--help] [--dpi DPI] [--large]

    --dpi DPI   scale theme to this DPI
    --help      show help and exit
    --large     use large fonts
EOF
}

while true; do
    case "$1" in
        --dpi)
            DPI="$2"
            shift
            shift
            ;;
        --large)
            LARGE=1
            shift
            ;;
        --help)
            show_help
            exit
            ;;
        *)
            break
    esac
done

if [ -z "$DPI" ]; then
    . "$CURRENT_THEME"
else
    if [[ $DPI -le 138 ]]; then
        CURSOR_SIZE=16

        FONT_NAME=Tamzen
        FONT_SIZE=9

        ANTIALIAS=false

        BORDER_WIDTH=1

        DUNST_GEOMETRY=300x5-0+18
        DUNST_MAX_ICON_SIZE=64

        SCALING_FACTOR=1
    else
        CURSOR_SIZE=48

        FONT_NAME='DejaVu Sans Mono'
        FONT_SIZE=10

        ANTIALIAS=true

        BORDER_WIDTH=3

        DUNST_GEOMETRY=800x5-0+40
        DUNST_MAX_ICON_SIZE=128

        SCALING_FACTOR=2
    fi

    if [[ -n "$LARGE" ]]; then
        FONT_NAME='DejaVu Sans Mono'
        FONT_SIZE=14

        ANTIALIAS=true
    fi
fi

# save settings
cat > "$CURRENT_THEME" << EOF
ANTIALIAS="$ANTIALIAS"
BORDER_WIDTH="$BORDER_WIDTH"
CURSOR_SIZE="$CURSOR_SIZE"
DPI="$DPI"
DUNST_GEOMETRY="$DUNST_GEOMETRY"
DUNST_MAX_ICON_SIZE="$DUNST_MAX_ICON_SIZE"
FONT_NAME="$FONT_NAME"
FONT_SIZE="$FONT_SIZE"
SCALING_FACTOR="$SCALING_FACTOR"
EOF

# changing org.gnome.desktop.interface cursor-size after xrdb steps
# fucks up i3's scaling...
gsettings set org.gnome.desktop.interface cursor-size $CURSOR_SIZE
gsettings set org.gnome.desktop.interface scaling-factor $SCALING_FACTOR
gsettings set org.gnome.settings-daemon.plugins.xsettings overrides \
    "{'Gdk/WindowScalingFactor': <$SCALING_FACTOR>, 'Gtk/CursorThemeSize': <$CURSOR_SIZE>}"

# update xresources
XRESOURCES_TEMP_FILE=/tmp/dpi_xresources
cat > $XRESOURCES_TEMP_FILE <<- EOF
*facename: ${FONT_NAME}
*fontsize: ${FONT_SIZE}
*font: xft:${FONT_NAME}:size=${FONT_SIZE}:antialias=${ANTIALIAS}:dpi=${DPI}
*boldFont:

rofi.font: ${FONT_NAME} ${FONT_SIZE}
rofi.dpi: ${DPI}
rofi.bw: ${BORDER_WIDTH}

Xft.dpi: ${DPI}

Xcursor.size: ${CURSOR_SIZE}
$(base16-xresources)
EOF
xrdb $XRESOURCES_TEMP_FILE
rm $XRESOURCES_TEMP_FILE

xrandr --dpi $DPI

termite-config-reload
hsetroot -solid $(xrdb -query | grep '*background' | awk '{print $2}')

pkill -9 dunst || true
dunst \
    -font "$FONT_NAME $FONT_SIZE" \
    -frame_width "$BORDER_WIDTH" \
    -max_icon_size "$DUNST_MAX_ICON_SIZE" \
    -geometry "$DUNST_GEOMETRY" &

# need to restart to ensure cursor size on status bar is updated
i3 restart &> /dev/null || true
