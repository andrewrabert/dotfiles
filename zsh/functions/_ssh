#compdef ssh

# Limit ssh completions to hosts only
zstyle ':completion:*:ssh:argument-1:*' tag-order hosts

# Override _ssh_hosts to use our custom parsing
_ssh_hosts() {
    local hosts config_lines

    #hosts=(${(f)"$(grep "^Host " ~/.ssh/config 2>/dev/null | awk '{for(i=2;i<=NF;i++) if($i !~ /[*?]/) print $i}')"})
    #
    [[ -f ~/.ssh/config ]] || return
    config_lines=(${(f)"$(<~/.ssh/config)"})

    # Strip leading whitespace and match lines starting with "Host" or "host"
    hosts=(${(M)${config_lines##[[:space:]]#}:#[Hh]ost *})
    # Remove "Host" keyword and following whitespace
    hosts=(${hosts#[Hh]ost[[:space:]]})
    # Convert all whitespace to single spaces (flattens multiple hosts per line)
    hosts=(${hosts//[[:space:]]/ })
    # Trim trailing whitespace from each hostname
    hosts=(${hosts%%[[:space:]]#})
    # Remove entries containing wildcards
    hosts=(${hosts:#*[*?]*})

    _describe 'SSH hosts' hosts "$@"
}

# Generic function to source the parent completion in fpath
_source_parent_completion() {
    local completion_name="$1"
    local current_file="${(%):-%x}"
    local found_current=false

    for dir in "$fpath[@]"; do
        local candidate="$dir/$completion_name"
        [[ -f "$candidate" ]] || continue
        if [[ "$candidate" == "$current_file" ]]; then
            found_current=true
        elif $found_current; then
            source "$candidate"
            return
        fi
    done
    return 1
}

# Source the original _ssh completion
_source_parent_completion "_ssh"
